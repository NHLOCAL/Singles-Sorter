::מטרת הסקריפט היא לסדר שירים בודדים בתיקיות לפי אמנים
::הסקריפט מיועד לציבור החרדי ובשל כך הדאטה שלו מותאמת לציבור זה
::קרדיט: nh.local11@gmail.com

@echo off
::הגדרות של שפה, צבע, כותרת וגודל החלון
::ועוד מספר משתנים חשובים
chcp 1255>nul
set "VER=12.0"
title %VER% מסדר הסינגלים
MODE CON COLS=80 lines=18
::קביעת משתנה למיקום קובץ הדאטה
set "csv-file=%~dp0singer-list.csv"
set personal-csv-file="%~dp0personal-singer-list.csv"
if not exist %personal-csv-file% set personal-csv-file=

:call-num
::בדיקת מספר הזמרים הקיים כעת בסקריפט
::הדבר נצרך לצורך חישוב הזמן שעבר
::ולצורך הפונקציה של הוספת זמרים עצמאית
for /f "tokens=1,2* delims=" %%n in ('type "%csv-file%" ^| find /c ","') do set ab=%%n
if defined personal-csv-file for /f "tokens=1,2* delims=" %%n in ('type "%personal-csv-file%" ^| find /c ","') do set ac=%%n
set/a abc=ab+ac

::מסדר הסינגלים עצמו
:mesader-singels
color f1


:beginning
cls
set source_path=%1

::קביעת התיקיה הנוכחית לתיקית המקור
cd /d %source_path%

::במידה והוכנס פרמטר "clean"
::יבוצע ניקוי קבצים בלבד
if [%2]==[-clean] set "clear_heb=True" & goto :intro

:target_folder
cls
set "h=%~2"

::קביעת משתנים לצורך הגדרות המשתמש
::================================

::הגדרת משתנה ליצירת תיקית סינגלים פנימית
set in_folder_creating=%5
if "%in_folder_creating%"=="True" (
set sing_heb=True
set "s=\סינגלים"
)else (
set "sing_heb=False"
set s=
)

::קביעת משתנה ליצירת תיקיות ראשיות בחלוקה לא' ב'
set abc_dirs_creating=%7
if "%abc_dirs_creating%"=="True" (set "abc_heb=True") else (set "abc_heb=False")

::הגדרת משתנה העברה או העתקה
set copy_moving=%3
if "%copy_moving%"=="True" (
set c_or_m=xcopy
set par=/y
set "msg=וקתעוהש"
set cm_heb=הקתעה
) else (
set par=
set c_or_m=move
set "msg=ורבעוהש"
set cm_heb=הרבעה
)

::הגדרת משתנה ליצירת תיקיות חדשות או העברה לתיקיות קיימות בלבד
set creating_folder=%6
if "%creating_folder%"=="True" (set fixed_heb=True) else (set "fixed_heb=False")

::הגדרת משתנה לסריקת תיקיות משנה
set tree_scanning=%4
if "%tree_scanning%"=="True" (set "dir_heb=False") else (set "dir_heb=True")



:intro
::ביצוע ניקוי לשמות הקבצים
::אם הוגדר כך על ידי המשתמש
cls
if "%clear_heb%"=="True" (
for /r %%i in (*) do (
cls
echo.
echo                           ...םיצבקה תומש לש יוקינ עצבמ
set "file=%%~ni"
set "ext=%%~xi"
call :clear-func
)
)

::יציאה במקרה והוגדר ניקוי קבצים בלבד
if [%2]==[-clean] pause & exit
::המשך במקרה ולא
goto :preparing

:clear-func
::הפונקציה מבצעת ניקוי של שמות הקבצים
set "new_filename=%file:_= %"
set "new_filename=%new_filename: -מייל מיוזיק=%"
set "new_filename=%new_filename: - ציצו במייל=%"
set "new_filename=%new_filename: -מייל מיוזיק=%"
set "new_filename=%new_filename:-חדשות המוזיקה=%"
set "new_filename=%new_filename: - חדשות המוזיקה=%"
set "new_filename=%new_filename: - ציצו=%"
set "new_filename=%new_filename: מוזיקה מכל הלב=%"
set "new_filename=%new_filename: - מייל מיוזיק=%"
ren "%file%%ext%" "%new_filename%%ext%"
exit /b




:preparing
cls
echo.
echo                                    ...דבוע
::מספר הגדרות משתנים חשובות

::הגדרת סריקת תיקיות משנה לפי העדפות המשתמש
if "%dir_heb%"=="True" (set tree=/r) else (set tree=)
::איפוס מספרים עבור חישוב אחוזים
set/a d=1
::איפוס משתנה עבור סריקה מתקדמת לפי אמן
set pro_scan=False
:start
::פקודת הפור הראשית שסורקת שמות קבצים
for /f "usebackq tokens=1,2 delims=,"  %%i in ("%csv-file%",%personal-csv-file%) do (
set a=%%i
set c=%%j
call :sort-func
)

goto :finish

:sort-func
::הגדרת שם הזמר לחיפוש
set a=*%a: =?%*.*

::הגדרת משתנה להצגת מספר אחוזים שהושלמו
set/a en=%d%00/ab
if not "%en%"=="%enb%" cls & echo. & echo                                    ...דבוע & echo. & echo. & echo. & echo                               ...ומלשוה םיזוחא %en%
set/a enb=%d%00/ab

::קובע אם יווצרו תקיות ראשיות לפי א ב
if "%abc_heb%"=="True" set w=%c:~0,1%\

::קביעת נתיב יעד עם מספר משתנים - בהתאם להגדרות המשתמש
set b="%h%\%w%%c%%s%"

::יצירת תיקית זמר בכפוף לכמה תנאים
set xx=v
set ss=z
for %tree% %%c in ("%a%") do if exist %%c set ss=ss
if "%fixed_heb%"=="True" if not exist "%h%\%w%%c%" set ss==z
if %ss%==ss md %b%

::הגדרת מחיקת קבצי מקור לאחר העתקה אם הוגדר כך
if %c_or_m%==del set b= & set par=/q

::העתקת הסינגלים בכפוף לכמה תנאים
for %tree% %%d in ("%a%") do if exist %%d set xx=xx

if defined b if exist %b% (set xx=xx) else (set xx=vv)

if "%xx%"=="xx" for %tree% %%e in (%a%) do %c_or_m% %par% "%%e" %b%>>םוכיס

::מעבר למספר הבא לצורך חישוב ההתקדמות
set/a d=d+1

::יציאה מהפונקציה וחזרה לפקודת הפור
exit /b


:finish
cls
echo.
echo.
echo                                 %VER% םילגניסה רדסמ
echo                                       *****
echo.
echo.
if %c_or_m%==del echo                                   !וקחמנ םיצבקה & echo. & del םוכיס & goto :intro_pro
if exist םוכיס (echo                                 :%msg% םיצבקה רפסמ & find /c "1" םוכיס
) else (
echo                                   !רבד אצמנ אל
)
if exist םוכיס (del םוכיס) else (goto :intro_pro)
echo.
::אם התבצעה העתקה ניתנת אפשרות למחוק את קבצי המקור
if not "%pro_scan%"=="True" if %c_or_m%==xcopy (
echo.
echo                 [2] תעכ שקה םיירוקמה םיצבקה תא קוחמל ןיינועמ התא םא
echo                         [1] שקה םתוא רומשל ןיינועמ התא םא
echo.
echo               !הקיחמב רוחבל רוסא היקית התוא םה דעיהו ביתנה םא !תוריהז
choice /c 12>nul
if errorlevel 2 set c_or_m=del & goto preparing
if errorlevel 1 goto :intro_pro
)

:intro_pro
::מתבצע מעבר לפונקצייה המתקדמת
goto :pro_scanner

:pause
echo.
echo.
echo.
echo ================================================================================
echo                               !החלצהב םייתסה ךילהתה
pause>nul
exit


:pro_scanner
::הוספת מיקום התוכנה החיצונית למשתנה הסביבה
path "%AppData%\singles-sorter";%path%

cls
echo.
echo                                    ...דבוע
echo.
echo.
cd /d "%~dp0"
pro_func_release.exe %source_path% "%h%" %c_or_m% %abc_heb% %fixed_heb% %in_folder_creating% %dir_heb%


echo.
echo.
echo.
echo                             !בוט לזמ !לכה ונמייס 
echo.
echo                      ןולחה תא רוגסל ידכ והשלכ שקמ לע שקה
pause>nul

::לבינתיים יציאה מידית
exit

set "artist_heb=False"
set pro_scan=True
goto :finish



::קרדיט: nh.local11@gmail.com